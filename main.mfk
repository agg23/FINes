import nes_lib
import custom_random
import graphics
import graphics_buffer

import ui/commandbar
import tank/tank

import fish
import entity/money

byte in_nmi = 0
byte nmi_fail = 0

bool new_frame = false

void main() {
  init_random()
  init_tank()
  init_fish()
  init_graphics()

  render_money()

  while(true) {
    // Wait for NMI to complete rendering frame
    while (new_frame) {}

    commandbar_tick()
    fish_tick()
    sprite_tick()

    // Wait for sprite0 clear
    while (ppu_status & %01000000 != 0) {}

    new_frame = true
  }
}

void nmi() {
  if in_nmi != 0 {
    // Overran NMI
    nmi_fail = 1
    asm {
      kil
    }
  }

  in_nmi = 1

  if (new_frame) {
    set_color_effect(clear)
    sprite_render()

    process_buffered_writes()

    read_ppu_status()
    ppu_set_scroll(0, 0)

    // Wait for frame to start
    while (ppu_status & %01000000 != 0) {}
    // // Wait for sprite0
    while (ppu_status & %01000000 == 0) {}
    set_color_effect(blue)

    new_frame = false
  }

  in_nmi = 0
}

void irq() {

}