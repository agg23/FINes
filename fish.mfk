import random
import custom_random
import movement_lib

import tank/state

struct Fish {
  byte x,
  byte y,
  word velocity_x,
  word velocity_y
}

Fish fish

const byte FISH_MAX_COUNT = 20

array(Fish) fish_instances [FISH_MAX_COUNT]
byte active_fish_count = 0

const word FISH_MAX_VELOCITY_PER_TICK = $100
const byte FISH_MAX_VELOCITY = 2
const byte FISH_DECAY_VELOCITY_PER_TICK = $20

void init_fish() {
  spawn_fish()
  spawn_fish()
  spawn_fish()
}

void spawn_fish() {
  if (active_fish_count >= FISH_MAX_COUNT) {
    return
  }

  Fish fish
  fish.x = $80
  fish.y = $20

  fish_instances[active_fish_count] = fish

  active_fish_count += 1
}

void fish_tick() {
  byte temp
  byte i
  byte random_direction
  byte x
  byte y
  word velocity_x
  word velocity_y
  pointer.Fish fish

  for i,0,until,active_fish_count {
    fish = fish_instances[i].pointer

    x = fish->x
    y = fish->y
    velocity_x = fish->velocity_x
    velocity_y = fish->velocity_y

    temp = rand()

    if (temp > $E0) {
      random_direction = rand()

      if random_direction < $10 {
        // Low chance to move vertically
        if random_direction < $5 {
          // Up
          velocity_y -= FISH_MAX_VELOCITY_PER_TICK
        } else {
          // Down
          velocity_y += FISH_MAX_VELOCITY_PER_TICK
        }
      } else {
        if random_direction < $78 {
          // Left
          velocity_x -= FISH_MAX_VELOCITY_PER_TICK
        } else {
          // Right
          velocity_x += FISH_MAX_VELOCITY_PER_TICK
        }
      }

      cap_velocity(velocity_x.hi, velocity_x.lo, FISH_MAX_VELOCITY)
      cap_velocity(velocity_y.hi, velocity_y.lo, FISH_MAX_VELOCITY)
    }

    x += sbyte(velocity_x.hi)
    y += sbyte(velocity_y.hi)

    cap_fish_location_to_active_tank(x, y)

    fish->x = x
    fish->y = y

    // decay_velocity(velocity_x.hi, velocity_x.lo, FISH_DECAY_VELOCITY_PER_TICK)
    // decay_velocity(velocity_y.hi, velocity_y.lo, FISH_DECAY_VELOCITY_PER_TICK)

    fish->velocity_x = velocity_x
    fish->velocity_y = velocity_y
  }
}

inline void render_fish() {
  byte i
  byte direction_mask
  Fish fish
  for i,0,until,active_fish_count {
    fish = fish_instances[i]

    if (sbyte(fish.velocity_x.hi) >= 0) {
      direction_mask = 0
    } else {
      direction_mask = %01000000
    }

    oam_buffer[oam_index] = fish.y
    oam_buffer[oam_index + 1] = $80
    oam_buffer[oam_index + 2] = direction_mask
    oam_buffer[oam_index + 3] = fish.x

    oam_index += 4
  }
}