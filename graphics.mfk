import nes_graphics

import palette

import fish/fish

enum ColorEffect {
    red,
    blue,
    green,
    grey,
    red_blue,
    clear
}

byte oam_index = 0

bool drawing_enabled

bool enable_sprite0

// enable NMI, sprites from Pattern Table 0, background from Pattern Table 1
const byte standard_ppu_ctrl = %10010000
// Same as above, but increment address by 32
const byte vertical_ppu_ctrl = %10010100

// enable sprites, enable background, no clipping on left side
const byte default_mask = %00011110
inline void init_graphics() {
  ppu_wait_vblank()

  //for now, turn off the screen and nmi
  read_ppu_status()
  ppu_ctrl = 0
  ppu_mask = 0

  clear_nametables()

  init_sprites()
}

inline void enable_graphics() {
  // TODO: Figure out initial load flicker
  // Reset vblank flag
  read_ppu_status()
  ppu_ctrl = standard_ppu_ctrl
  read_ppu_status()
  ppu_set_scroll(0, 0)
  ppu_mask = default_mask

  drawing_enabled = true
}

inline void wait_for_sprite0_nmi() {
  if (enable_sprite0) {
    wait_for_sprite0_clear()
    // Wait for sprite0
    while (ppu_status & %01000000 == 0) {}
  }
}

inline void wait_for_sprite0_clear() {
  if (enable_sprite0) {
    // Wait for frame to start
    while (ppu_status & %01000000 != 0) {}
  }
}

inline void clear_nametables() {
  byte i
  byte j

  read_ppu_status()
  ppu_set_addr(ppu_nametable0)

  for i,0,until,8 {
    for j,0,until,32 {
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
      ppu_write_data(0)
    }
  }
}

inline void init_sprites() {
  oam_index = 0
  clear_remaining_oam()
}

inline void sprite_tick() {
  oam_index = 0
}

inline void render_sprite0() {
  // Disabled, as it is not currently used
  // oam_buffer[oam_index] = 8 * 5
  // oam_buffer[oam_index + 1] = $1
  // // Palette 2
  // oam_buffer[oam_index + 2] = $2
  // oam_buffer[oam_index + 3] = 8

  // oam_index += 4
}

inline void clear_remaining_oam() {
  byte i
  for i,oam_index,until,255 {
    if (i & %00000011) == 0 {
      //each sprite takes up 4 bytes, and we want to edit
      //the y position of each sprite (0th byte)
      //so we use the %00000011 mask to write every 4th byte (every 0th sprite byte)
      
      oam_buffer[i] = $ef // move the sprite off screen
    } else {
      oam_buffer[i] = 0
    }
  }
}

inline void sprite_render() {
  //Push sprite information to the PPU through DMA transfer
  ppu_oam_dma_write(oam_buffer.addr.hi)
}

inline void set_color_effect(ColorEffect effect) {
  if (effect == red) {
    ppu_mask = default_mask | %00100000
  } else if (effect == green) {
    ppu_mask = default_mask | %01000000
  } else if (effect == blue) {
    ppu_mask = default_mask | %10000000
  } else if (effect == grey) {
    ppu_mask = default_mask | %00000001
  } else if (effect == red_blue) {
    ppu_mask = default_mask | %10100000
  } else {
    ppu_mask = default_mask
  }
}

/**
 * Sets PPU_CTRL to the proper selected nametable
 */
inline void set_ppu_ctrl(bool right, bool bottom) {
  byte temp_ctrl
  temp_ctrl = standard_ppu_ctrl

  if right {
    temp_ctrl = temp_ctrl | %1
  }

  if bottom {
    temp_ctrl = temp_ctrl | %10
  }

  read_ppu_status()
  ppu_ctrl = temp_ctrl
}

segment(chrrom) const array graphics @ $0000 = file("tiles2.chr")
