import entity/health

import plants/anubias
import plants/babytears
import plants/rotala

enum PlantType {
  rotala
  anubias
  babytears
}

enum PlantSize {
  small
  medium
  large
}

enum PlantPalette {
  palette_red,
  palette_green,
  palette_unset
}

const byte MAX_PLANT_COUNT = 10
const byte MAX_PLANT_COLUMNS = 30

array(bool) plant_active [MAX_PLANT_COUNT]
array(PlantType) plant_type [MAX_PLANT_COUNT]
array(byte) plant_x [MAX_PLANT_COUNT]
array(PlantSize) plant_size [MAX_PLANT_COUNT]
array(PlantPalette) plant_current_palettes [MAX_PLANT_COLUMNS >> 1]
array(byte) plant_current_x [MAX_PLANT_COLUMNS]

byte plant_min_x
byte plant_max_x_bound

void init_plants() {
  byte i
  plant_min_x = active_tank.x_low >> 3
  plant_max_x_bound = (active_tank.x_high >> 3) - plant_min_x

  for i,0,until,MAX_PLANT_COLUMNS {
    plant_current_x[i] = $FF
  }

  for i,0,until,15 {
    plant_current_palettes[i] = palette_unset
  }

  add_plant(rotala, 6, large, $FF)

  add_plant(babytears, 8, small, $FF)
  add_plant(babytears, 10, medium, $FF)
  add_plant(babytears, 12, small, $FF)


  // TODO: Adding one more plant crashes the buffer renderer
  // add_plant(babytears, 14, medium)
  add_plant(anubias, 16, medium, $FF)
  // add_plant(babytears, 20, medium)
}

void add_plant(PlantType type, byte x, PlantSize size, byte max_width) {
  byte i
  byte j
  byte actual_width

  for i,0,until,MAX_PLANT_COUNT {
    if (not(plant_active[i])) {
      plant_active[i] = true
      plant_type[i] = type
      plant_x[i] = x
      plant_size[i] = size

      if (plant_expected_width[type] < max_width) {
        actual_width = plant_expected_width[type]
      } else if (max_width < plant_min_width[type]) {
        // Should not happen
        asm {
          kil
        }
        return
      } else {
        actual_width = max_width
      }

      for j,0,until,actual_width {
        plant_current_x[x + j] = i
        plant_current_palettes[(x + j) >> 1] = plant_type_palette[type]
      }
      
      return
    }
  }
}

void draw_plants() {
  byte i
  byte temp
  for i,0,until,MAX_PLANT_COUNT {
    if (plant_active[i]) {
      draw_plant_i(i, plant_x[i])
    }
  }
}

void draw_plant_at_x(byte x) {
  byte i
  for i,0,until,MAX_PLANT_COUNT {
    if (plant_active[i] && plant_x[i] == x) {
      draw_plant_i(i, x)
    }
  }
}

void draw_plant_i(byte i, byte x) {
  draw_plant_of_type(plant_type[i], plant_size[i], x, true)
}

void draw_plant_of_type(PlantType type, PlantSize size, byte x, bool write_attr) {
  array(byte) data [10]

  if (type == rotala) {
    draw_rotala(size, x, data.pointer, write_attr)
  } else if (type == babytears) {
    draw_babytears(size, x, data.pointer, write_attr)
  } else if (type == anubias) {
    draw_anubias(size, x, data.pointer, write_attr)
  }
}

byte build_attribute_table(byte x, PlantType type) {
  PlantPalette temp_palette
  byte prev_palette_number
  byte next_palette_number
  byte output

  if ((x + plant_min_x) & 2 != 0) {
    // Odd position, need to consider previous palette
    if (x < 3) {
      // Prev is wall
      prev_palette_number = 0
    } else {
      temp_palette = plant_current_palettes[(x >> 1) - 1]

      prev_palette_number = palette_number_for_palette(temp_palette)
    }

    next_palette_number = palette_number_for_palette(plant_type_palette[type])
  } else {
    // Even position, need to consider next palette
    if (x > plant_max_x_bound - 3) {
      // Next is wall
      next_palette_number = 0
    } else {
      temp_palette = plant_current_palettes[(x >> 1) + 1]

      next_palette_number = palette_number_for_palette(temp_palette)
    }

    prev_palette_number = palette_number_for_palette(plant_type_palette[type])
  }

  output = next_palette_number
  output = (output << 2) | prev_palette_number
  output = (output << 2) | next_palette_number
  output = (output << 2) | prev_palette_number

  return output
}

inline byte palette_number_for_palette(PlantPalette type) {
  if (type == palette_green) {
    return 1
  } else if (type == palette_red) {
    return 2
  }

  return 0
}

void plant_tick() {
  byte i

  for i,0,until,MAX_PLANT_COUNT {
    if (not(plant_active[i])) {
      continue
    }

    if (rand() < $60) {
      decrease_ammonia(1)
    }
  }
}

inline word plant_tile_height_from_floor(byte height) {
  return ((word(active_tank.y_high) >> 3) - height + 1) * 32
}

// rotala, anubias, babytears
const array(byte) plant_expected_width[PlantType] = [1, 3, 2]
const array(byte) plant_min_width[PlantType] = [1, 3, 1]
const array(byte) plant_max_editor_height[PlantType] = [3, 4, 2]
const array(PlantPalette) plant_type_palette[PlantType] = [palette_red, palette_green, palette_green]