import palette
import input
import bank
import entity/money

import ui/screen

import plants/plants
import tank/state

byte plant_editor_timer
bool active_plant_flash
/**
* Optimization to prevent writing attr updates unless the plant just moved
*/
bool active_plant_write_attr
PlantType plant_editor_type
byte plant_editor_x
byte plant_editor_width

void start_plant_editing(PlantType type) {
  plant_editor_timer = 0
  active_plant_flash = true
  active_plant_write_attr = true
  plant_editor_type = type
  plant_editor_width = plant_expected_width[type]
  plant_editor_x = 0

  set_screen(plant_editor)
}

void stop_plant_editing() {
  set_screen(shop)
}

inline bool plant_editor_tick() {
  array(byte) data [10]

  byte temp

  if (input_can_be_consumed) {  
    if (input_b != 0) {
      refund(plant_price(plant_editor_type))

      stop_plant_editing()
      deactivate_commandbar_selection()
      return true
    }
    if (input_a != 0) {
      add_plant(plant_editor_type, plant_editor_x, small, $FF)
      stop_plant_editing()
      deactivate_commandbar_selection()
      return true
    }
    if (input_dx < 0) {
      // Left

      if (plant_editor_x > 0) {
        clear_plant_column()
        temp = find_prev_x(plant_editor_x, plant_editor_type)

        if (temp != $FF) {
          // Found new x
          plant_editor_x = temp
        }

        plant_editor_timer = 0
        active_plant_flash = true
        active_plant_write_attr = true

        return true
      }
    } else if (input_dx > 0) {
      // Right
      if (plant_editor_x < plant_max_x_bound - 1) {
        clear_plant_column()
        temp = find_next_x(plant_editor_x, plant_editor_type)

        if (temp != $FF) {
          // Found new x
          plant_editor_x = temp
        }

        plant_editor_timer = 0
        active_plant_flash = true
        active_plant_write_attr = true

        return true
      }
    }
  }

  if (plant_editor_timer == 0) {
    if (active_plant_flash) {
      draw_plant_of_type(plant_editor_type, large, plant_editor_x, active_plant_write_attr)

      active_plant_flash = false
      active_plant_write_attr = false
    } else {
      clear_plant_column()

      active_plant_flash = true
    }

    plant_editor_timer = 10
  } else {
    plant_editor_timer -= 1
  }

  return false
}

inline byte find_next_x(byte x, PlantType type) {
  byte i
  byte count

  for i, x + 1,until,plant_max_x_bound {
    if (plant_current_x[i] == $FF) {
      // Free space. Check if plant fits
      count = available_width_at_x(i, type)

      if (count >= plant_min_width[type]) {
        // Plant fits
        return i
      }
    }
  }

  return $FF
}

byte find_prev_x(byte x, PlantType type) {
  byte i
  byte count

  for i,x - 1,downto,0 {
    if (plant_current_x[i] == $FF) {
      // Free space. Check if plant fits
      count = available_width_before_x(i, type)

      if (count >= plant_min_width[type]) {
        // Plant fits
        return i
      }
    }
  }

  return $FF
}

inline byte available_width_at_x(byte x, PlantType type) {
  byte i
  byte count
  PlantPalette plant_palette
  count = 0
  for i,0,until,plant_expected_width[type] {
    plant_palette = plant_current_palettes[(i + x) >> 1]
    if (plant_palette == palette_unset || plant_palette == plant_type_palette[type]) {
      count += 1
    } else {
      return count
    }
  }

  return count
}

inline byte available_width_before_x(byte x, PlantType type) {
  byte i
  byte count
  PlantPalette plant_palette
  count = 0
  for i,0,until,plant_expected_width[type] {
    plant_palette = plant_current_palettes[x >> 1]
    if (plant_palette == palette_unset || plant_palette == plant_type_palette[type]) {
      count += 1
    } else {
      return count
    }
  }

  return count
}

inline void clear_plant_column() {
  byte i
  word height_from_floor
  array(byte) data [10]

  height_from_floor = plant_tile_height_from_floor(plant_max_editor_height[plant_editor_type])

  for i,0,until,10 {
    data[i] = 0
  }

  for i,0,until,plant_editor_width {
    add_buffered_write(ppu_nametable0 + height_from_floor + plant_editor_x + plant_min_x + i, true, data.pointer, plant_max_editor_height[plant_editor_type], false)
  }
}

void draw_plant_editor() {
  draw_aquarium()

  load_plant_palettes()

  set_bank_0()
}

inline void load_plant_palettes() {
  add_buffered_write(ppu_palette_ram + 4, false, plant_palette.pointer, 8, true)
}