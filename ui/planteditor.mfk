import palette
import input
import bank
import entity/money

import ui/screen

import plants/plants
import tank/state

byte plant_editor_timer
bool active_plant_flash
/**
* Optimization to prevent writing attr updates unless the plant just moved
*/
bool active_plant_write_attr
PlantType plant_editor_type
byte plant_editor_x
byte plant_editor_width

void start_plant_editing(PlantType type) {
  word available_position
  plant_editor_timer = 0
  active_plant_flash = true
  active_plant_write_attr = true
  plant_editor_type = type

  available_position = find_next_x(-1, type)
  plant_editor_x = available_position.hi
  plant_editor_width = available_position.lo

  set_screen(plant_editor)
}

void stop_plant_editing() {
  set_screen(shop)
  init_plants_shop()
}

inline bool plant_editor_tick() {
  array(byte) data [10]

  word wtemp

  if (input_can_be_consumed) {  
    if (input_b != 0) {
      refund(plant_price(plant_editor_type))

      stop_plant_editing()
      return true
    }
    if (input_a != 0) {
      add_plant(plant_editor_type, plant_editor_x, small, plant_editor_width)
      stop_plant_editing()
      return true
    }
    if (input_dx < 0) {
      // Left

      if (plant_editor_x > 0) {
        clear_plant_column()
        wtemp = find_prev_x(plant_editor_x, plant_editor_type)

        if (wtemp.hi != $FF) {
          // Found new x
          plant_editor_x = wtemp.hi
          plant_editor_width = wtemp.lo
        }

        plant_editor_timer = 0
        active_plant_flash = true
        active_plant_write_attr = true

        return true
      }
    } else if (input_dx > 0) {
      // Right
      if (plant_editor_x < plant_max_x_bound) {
        clear_plant_column()
        wtemp = find_next_x(plant_editor_x, plant_editor_type)

        if (wtemp.hi != $FF) {
          // Found new x
          plant_editor_x = wtemp.hi
          plant_editor_width = wtemp.lo
        }

        plant_editor_timer = 0
        active_plant_flash = true
        active_plant_write_attr = true

        return true
      }
    }
  }

  if (plant_editor_timer == 0) {
    if (active_plant_flash) {
      draw_plant_of_type(plant_editor_type, large, plant_editor_x, active_plant_write_attr, plant_editor_width, 0)

      active_plant_flash = false
      active_plant_write_attr = false
    } else {
      clear_plant_column()

      active_plant_flash = true
    }

    plant_editor_timer = 10
  } else {
    plant_editor_timer -= 1
  }

  return false
}

/**
* @returns hi(x position), lo(plant width)
*/
inline word find_next_x(byte x, PlantType type) {
  byte i
  byte count
  word output

  output.hi = $FF

  for i, x + 1,until,plant_max_x_bound + 1 {
    if (plant_at_x[i] == $FF) {
      // Free space. Check if plant fits
      count = available_width_at_x(i, type)

      if (count >= plant_min_width[type]) {
        // Plant fits
        output.hi = i
        output.lo = count
        return output
      }
    }
  }

  return output
}

/**
* @returns hi(x position), lo(plant width)
*/
inline word find_prev_x(byte x, PlantType type) {
  byte i
  byte count
  word output

  output.hi = $FF

  for i,x - 1,downto,0 {
    if (plant_at_x[i] == $FF) {
      // Free space. Check if plant fits
      count = available_width_at_x(i, type)

      if (count >= plant_min_width[type]) {
        // Plant fits
        output.hi = i
        output.lo = count
        return output
      }
    }
  }

  return output
}

inline byte available_width_at_x(byte x, PlantType type) {
  byte i
  byte j
  byte count
  PlantPalette plant_palette
  count = 0
  for i,0,until,plant_expected_width[type] {
    if (x + i > plant_max_x_bound || plant_at_x[x + i] != $FF) {
      // Existing plant in this location
      return count
    }

    plant_palette = plant_current_palettes[(x + i) >> 1]
    if (plant_palette == palette_unset || plant_palette == plant_type_palette[type]) {
      count += 1
    } else {
      return count
    }
  }

  return count
}

inline void clear_plant_column() {
  byte i
  word height_from_floor
  array(byte) data [10]

  height_from_floor = plant_tile_height_from_floor(plant_max_editor_height[plant_editor_type], 0)

  for i,0,until,10 {
    data[i] = 0
  }

  for i,0,until,plant_editor_width {
    add_buffered_write(ppu_nametable0 + height_from_floor + plant_editor_x + plant_min_x + i, true, data.pointer, plant_max_editor_height[plant_editor_type], false)
  }
}

void draw_plant_editor() {
  draw_aquarium()

  load_plant_palettes()

  set_bank_0()
}

inline void load_plant_palettes() {
  add_buffered_write(ppu_palette_ram + 4, false, plant_palette.pointer, 8, true)
}