import nes_joy
import fish

import ui/planteditor

import entity/inspect
import entity/money

const byte commandbar_item_count = 3

const byte input_timer_reset = 10

byte selected_command_item = 0
bool selection_is_active
byte input_timer = 0

void activate_feed() {
  start_plant_editing()
}

void end_feed() {
  stop_plant_editing()
}

void activate_fish_inspect() {
  start_inspect()
  deactivate_commandbar_selection()
}

void activate_spawn() {
  if (purchase(10)) {
    spawn_fish(tetra, false, $80, $20)
  }

  render_money()
  deactivate_commandbar_selection()
}

bool noop(byte i) {
  return false
}

const array(function.void.to.void) menuitem_actions = [activate_feed.pointer, activate_fish_inspect.pointer, activate_spawn.pointer]
const array(function.byte.to.bool) menuitem_ticks = [plant_editor_tick.pointer, noop.pointer, noop.pointer]
const array(function.byte.to.bool) menuitem_cleanup = [end_feed.pointer, noop.pointer, noop.pointer]

void commandbar_tick() {
  read_joy1()

  if (selection_is_active) {
    // TODO: Handle B back button

    if (call(menuitem_ticks[selected_command_item], input_timer)) {
      input_timer = input_timer_reset
    }
  } else {
    if (input_timer == 0) {
      if input_dx < 0 {
        // Left
        if selected_command_item > 0 {
          selected_command_item -= 1
        }
        input_timer = input_timer_reset
      } else if input_dx > 0 {
        // Right
        if selected_command_item < commandbar_item_count - 1 {
          selected_command_item += 1
        }
        input_timer = input_timer_reset
      }

      if input_a != 0 {
        selection_is_active = true
        call(menuitem_actions[selected_command_item])

        input_timer = input_timer_reset
      }
    }
  }

  if input_timer > 0 {
    input_timer -= 1
  }
}

inline void deactivate_commandbar_selection() {
  selection_is_active = false
}

void render_commandbar() {
  byte x_low
  byte x_high
  byte y_low
  byte y_high
  x_low = 8 - 3 + 24 * selected_command_item
  y_low = 8 - 4
  x_high = x_low + 8 + 6
  y_high = 24 - 6

  oam_buffer[oam_index] = y_low
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = 0
  oam_buffer[oam_index + 3] = x_low

  oam_index += 4

  oam_buffer[oam_index] = y_low
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %01000000
  oam_buffer[oam_index + 3] = x_high

  oam_index += 4

  oam_buffer[oam_index] = y_high
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %10000000
  oam_buffer[oam_index + 3] = x_low

  oam_index += 4

  oam_buffer[oam_index] = y_high
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %11000000
  oam_buffer[oam_index + 3] = x_high

  oam_index += 4
}

const array(byte) commandbar_background = [
  $4, $80, $81, $4, $82, $83, $4, $84,
  $85, $4, $4, $4, $4, $4, $4, $4,
  $4, $4, $4, $4, $4, $4, $4, $4,
  $4, $4, $4, $4, $4, $4, $4, $4,
  //
  $4, $90, $91, $4, $92, $93, $4, $94,
  $95]