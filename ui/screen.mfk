import graphics_buffer

import ui/aquarium
import ui/planteditor
import ui/shop/shop

enum Screen {
  aquarium,
  plant_editor,
  shop
}

bool buffer_screen_draw = false
Screen currentScreen

bool screen_tick() {
  return [currentScreen] {
    aquarium @ aquarium_tick
    plant_editor @ plant_editor_tick
    shop @ shop_tick
  }
}

void draw_screen() {
  return [currentScreen] {
    aquarium @ draw_aquarium
    plant_editor @ draw_plant_editor
    shop @ draw_shop
  }
}

void set_screen_forcibly(Screen new_screen) {
  currentScreen = new_screen
  buffer_screen_draw = true
}

void set_screen(Screen new_screen) {
  if (new_screen == currentScreen) {
    return
  }

  currentScreen = new_screen
  buffer_screen_draw = true
}

void screen_transfer_tick() {
  // Wait for NMI to disable rendering and NMIs
  // Draw new screen
  drawing_enabled = false
  buffer_screen_draw = false
  read_ppu_status()
  ppu_ctrl = standard_ppu_ctrl
  ppu_mask = 0

  ppu_wait_vblank()

  clear_buffer()
  draw_screen()
  process_buffered_writes()

  ppu_wait_vblank()

  enable_graphics()
}