import graphics

import bank
import custom_random
import entity/money

import ui/shop/render

import fish/fish

enum ShopSelectionState {
  tank,
  price,
  sex
}

enum ShopPriceSelectionState {
  any,
  price_selection_sex,
  male,
  female
}

const byte SHOP_COLUMN_COUNT = 3
const byte SHOP_ROW_COUNT = 2

byte current_shop_row
byte current_shop_column
ShopSelectionState shop_state
ShopPriceSelectionState shop_price_state

void init_shop() {
  current_shop_row = 0
  current_shop_column = 0
  shop_state = tank
  shop_price_state = any

  set_screen(shop)
}

void close_shop() {
  set_screen(aquarium)
}

inline bool shop_tick() {
  render_shop_fish()

  return [shop_state] {
    tank @ shop_tank_selection_tick
    price @ shop_price_selection_tick
  }
}

inline bool shop_tank_selection_tick() {
  bool temp
  temp = shop_tank_selection_input_tick()

  render_shop_tank_highlight()

  return temp
}

inline bool shop_tank_selection_input_tick() {
  if (input_can_be_consumed) {
    if (input_b != 0) {
      close_shop()
      deactivate_commandbar_selection()
      return true
    } else if (input_a != 0) {
      shop_state = price
      shop_price_state = any

      return true
    } else if (input_dx > 0 && current_shop_column < SHOP_COLUMN_COUNT - 1) {
      // Right
      current_shop_column += 1

      return true
    } else if (input_dx < 0 && current_shop_column > 0) {
      // Left
      current_shop_column -= 1

      return true
    } else if (input_dy > 0 && current_shop_row < SHOP_ROW_COUNT - 1) {
      // Bottom
      current_shop_row += 1

      return true
    } else if (input_dy < 0 && current_shop_row > 0) {
      // Top
      current_shop_row -= 1

      return true
    }
  }

  return false
}

inline bool shop_price_selection_tick() {
  bool temp
  temp = shop_price_selection_input_tick()

  render_shop_price_highlight()

  return temp
}

inline bool shop_price_selection_input_tick() {
  if (input_can_be_consumed) {
    if (input_b != 0) {
      if (shop_price_state == male || shop_price_state == female) {
        shop_price_state = price_selection_sex
      } else {
        shop_state = tank
      }

      return true
    } else if (input_a != 0) {
      if (shop_price_state == any) {
        purchase_fish()
      } else if (shop_price_state == price_selection_sex) {
        shop_price_state = male
      } else {
        // Male/female
        purchase_fish()
      }

      return true
    } else if (input_dy > 0 && shop_price_state == any) {
      // Bottom
      shop_price_state = price_selection_sex
      return true
    } else if (input_dy < 0 && shop_price_state == price_selection_sex) {
      // Top
      shop_price_state = any
      return true
    } else if (input_dx > 0 && shop_price_state == male) {
      shop_price_state = female
      return true
    } else if (input_dx < 0 && shop_price_state == female) {
      shop_price_state = male
      return true
    }
  }

  return false
}

void purchase_fish() {
  FishType type
  type = shop_fish_tanks[current_shop_row * 2 + current_shop_column]

  if (shop_price_state == any) {
    if (purchase(fish_any_price(type))) {
      // Purchase successful
      // TODO: Make better fish spawn position
      spawn_fish(type, random_bool(), $80, $20)
    }
  } else {
    if (purchase(fish_sexed_price(type))) {
      // Purchase successful
      // TODO: Make better fish spawn position
      spawn_fish(type, shop_price_state == male, $80, $20)
    }
  }
}

const array(FishType) shop_fish_tanks = [
  tetra, rasbora, tetra,
  tetra, tetra, tetra
]