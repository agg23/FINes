macro void render_cursor(byte x_low, byte x_high, byte y_low, byte y_high) {
  oam_buffer[oam_index] = y_low
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = 0
  oam_buffer[oam_index + 3] = x_low

  oam_index += 4

  oam_buffer[oam_index] = y_low
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %01000000
  oam_buffer[oam_index + 3] = x_high

  oam_index += 4

  oam_buffer[oam_index] = y_high
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %10000000
  oam_buffer[oam_index + 3] = x_low

  oam_index += 4

  oam_buffer[oam_index] = y_high
  oam_buffer[oam_index + 1] = $48
  oam_buffer[oam_index + 2] = %11000000
  oam_buffer[oam_index + 3] = x_high

  oam_index += 4
}

/**
* Draws the provided text, using the `x` position as the ending location of the write
*/
inline void draw_text_right(byte x, byte y, pointer.byte text, byte length) {
  draw_text(x - length, y, text, length, 0)
}

/**
* @param center_length If provided (non-zero), center the text within this length, preferring the left side
*/
inline void draw_text(byte x, byte y, pointer.byte text, byte length, byte center_length) {
  bool found
  byte i
  byte original_x

  original_x = x
  
  if (center_length != 0) {
    found = false
    // Check for new line
    for i,0,until,length {
      if (text[i] == $60) {
        x += (center_length - i) / 2
        found = true
        break
      }
    }

    if (not(found)) {
      x += (center_length - length) / 2
    }
  }

  read_ppu_status()
  ppu_set_addr(ppu_nametable0 + 32 * word(y) + word(x))

  for i,0,until,length {
    if (text[i] == $60) {
      draw_text_line(original_x, y + 1, text, i + 1, length - i - 1, center_length)
      return
    }
    ppu_write_data(text[i])
  }
}

void draw_text_line(byte x, byte y, pointer.byte text, byte offset, byte length, byte center_length) {
  byte i
  
  if (center_length != 0) {
    x += (center_length - length) / 2
  }

  read_ppu_status()
  ppu_set_addr(ppu_nametable0 + 32 * word(y) + word(x))

  for i,0,until,length {
    ppu_write_data(text[i + offset])
  }
}

inline void draw_number_text(byte x, byte y, byte number, byte length, bool draw_leading_zeros) {
  array(byte) data[5]

  generate_number_text(number, data.pointer, length, draw_leading_zeros)

  draw_text(x, y, data.pointer, length, 0)
}

inline void draw_number_word_text(byte x, byte y, word number, byte length, bool draw_leading_zeros) {
  array(byte) data[5]

  generate_number_word_text(number, data.pointer, length, draw_leading_zeros)

  draw_text(x, y, data.pointer, length, 0)
}

macro void generate_number_text(byte number, pointer.byte output, byte length, bool draw_leading_zeros) {
  bcdConvert(number)
  process_number_text(output, length, draw_leading_zeros)
}

macro void generate_number_word_text(word number, pointer.byte output, byte length, bool draw_leading_zeros) {
  bcdConvert(number)
  process_number_text(output, length, draw_leading_zeros)
}

/**
* @returns The number of non-blank digits
*/
byte process_number_text(pointer.byte output, byte length, bool draw_leading_zeros) {
  byte i
  byte last_nonzero_index

  if (draw_leading_zeros) {
    for i,0,until,length {
      output[length - 1 - i] = bcdResult[i] + $30
    }

    return 0
  } else {
    last_nonzero_index = 0

    for i,0,until,length {
      if (bcdResult[i] > 0) {
        last_nonzero_index = i
      }
    }

    for i,0,until,length {
      if (i == 0 || i < last_nonzero_index || bcdResult[i] != 0) {
        // After last nonzero index, don't draw any more zeros
        output[length - 1 - i] = bcdResult[i] + $30
      } else {
        output[length - 1 - i] = 0
      }
    }

    return last_nonzero_index + 1
  }
}

macro void build_attribute_table_entry(byte top_left, byte top_right, byte bottom_left, byte bottom_right, byte output) {
  output = bottom_right
  output = (output << 2) | bottom_left
  output = (output << 2) | top_right
  output = (output << 2) | top_left
}