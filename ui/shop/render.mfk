import ui/shared
import ui/shop/shop
import ui/shop/fish/render
import ui/shop/plants/render

import fish/allfish

inline void render_shop_tank_highlight() {
  byte x_low
  byte x_high
  byte y_low
  byte y_high
  x_low = 32 + current_shop_column * 8 * 8
  y_low = 24 + current_shop_row * 12 * 8
  x_high = x_low + 7 * 8
  y_high = y_low + 11 * 8 - 4

  render_cursor(x_low, x_high, y_low, y_high)
}

inline void render_shop_price_highlight() {
  byte x_low
  byte x_high
  byte y_low
  byte y_high
  x_low = 40 + current_shop_column * 8 * 8 - 4
  y_low = 12 * 8 + current_shop_row * 12 * 8 - 2

  if (shop_price_state != any) {
    y_low += 8
  } 
  
  if (shop_price_state == male) {
    x_high = x_low + 8 - 1
  } else if (shop_price_state == female) {
    x_low += 16
    x_high = x_low + 8 - 1
  } else {
    x_high = x_low + 6 * 8 - 1
  }

  y_high = y_low + 1

  render_cursor(x_low, x_high, y_low, y_high)
}

void draw_shop() {
  byte i
  byte j
  read_ppu_status()
  ppu_set_addr(ppu_nametable0)

  // Clear NT
  for i,0,until,30 {
    for j,0,until,32 {
      ppu_write_data(0)
    }
  }

  set_bank_1()

  enable_sprite0 = false

  return [shop_screen] {
    fish @ draw_shop_fish_screen
    plants @ draw_shop_plants_screen
  }
}
